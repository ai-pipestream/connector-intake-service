import org.gradle.plugins.signing.Sign

plugins {
    alias(libs.plugins.java)
    alias(libs.plugins.quarkus)
    alias(libs.plugins.maven.publish)
    id 'signing'
    id 'com.gradleup.nmcp' version '1.3.0'
    id 'pl.allegro.tech.build.axion-release' version '1.21.1'
    alias(libs.plugins.proto.toolchain)
}

// Semantic versioning with axion-release
scmVersion {
    tag {
        prefix = 'v'
    }
    checks {
        uncommittedChanges = false
        aheadOfRemote = false
        snapshotDependencies = false
    }
}

def pipestreamBomVersion = findProperty('pipestreamBomVersion')

// Proto toolchain configuration - fetches from Git and generates Java/gRPC code
// Using git-proto-workspace mode to handle cross-module dependencies
pipestreamProtos {
    sourceMode = 'git-proto-workspace'
    gitRepo = "https://github.com/ai-pipestream/pipestream-protos.git"
    gitRef = "main"
    generateMutiny = true
    generateDescriptors = true

    modules {
        // Just register module names - workspace mode handles dependencies automatically
        register("common")
        register("config")
        register("intake")
        register("admin")
        register("pipeline-module")
        register("repo")
        register("engine")  // For IntakeHandoff RPC only (intake is graph-agnostic)
    }
}

dependencies {
    implementation 'io.quarkus:quarkus-container-image-docker'
    implementation platform("ai.pipestream:pipestream-bom:${pipestreamBomVersion}")

    // Core Quarkus dependencies
    implementation libs.quarkus.arc
    implementation libs.quarkus.rest
    implementation libs.quarkus.grpc
    implementation 'io.grpc:grpc-services'  // gRPC health and reflection services
    implementation 'io.quarkus:quarkus-hibernate-orm-panache'
    implementation 'io.quarkus:quarkus-jdbc-postgresql'
    implementation 'io.quarkus:quarkus-flyway'
    implementation 'org.flywaydb:flyway-database-postgresql'
    implementation 'io.quarkus:quarkus-smallrye-health'
    implementation 'io.quarkus:quarkus-smallrye-openapi'
    implementation 'io.quarkus:quarkus-cache'

    // Service discovery and registration (matches connector-service baseline)
    implementation 'io.quarkus:quarkus-smallrye-stork'
    implementation 'io.smallrye.stork:stork-service-discovery-consul'
    // Note: stork-service-discovery-static-list is provided transitively by quarkus-dynamic-grpc
    implementation 'io.smallrye.reactive:smallrye-mutiny-vertx-consul-client'

    // Pipeline libraries (published to Maven Local)
    // Pipestream platform libraries (managed via BOM/catalog)
    implementation 'ai.pipestream:pipestream-quarkus-devservices'
    implementation 'ai.pipestream:quarkus-dynamic-grpc'
    implementation 'ai.pipestream:pipestream-service-registration'


    // Utilities
    implementation libs.password4j  // For API key verification
    implementation 'com.google.code.gson:gson:2.10.1'  // For JSON processing
    implementation libs.guava
    
    // Testing
    testImplementation 'io.quarkus:quarkus-junit5'
    // Mockito is allowed for isolated benchmark-style tests (no wiremock libs; wiremock is a Docker gRPC server)
    testImplementation 'io.quarkus:quarkus-junit5-mockito'
    testImplementation 'io.rest-assured:rest-assured'
    testImplementation platform('org.testcontainers:testcontainers-bom:2.0.1')
    testImplementation 'org.testcontainers:testcontainers'
    testImplementation 'org.testcontainers:junit-jupiter'
    testImplementation 'ai.pipestream:test-documents'
}

group = 'ai.pipestream'
version = "${scmVersion.version}"

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
    withJavadocJar()
    withSourcesJar()
}

test {
    systemProperty "java.util.logging.manager", "org.jboss.logmanager.LogManager"
    maxHeapSize = "12g"
    jvmArgs = ["-Xmx12g", "-XX:MaxMetaspaceSize=1g", "--add-opens", "java.base/java.lang=ALL-UNNAMED"]
}

compileJava {
    options.encoding = 'UTF-8'
    options.compilerArgs << '-parameters'
}

compileTestJava {
    options.encoding = 'UTF-8'
}

quarkusDev {
    jvmArgs = ["-Xmx10g", "-XX:MaxMetaspaceSize=1g", "--add-opens", "java.base/java.lang=ALL-UNNAMED"]
}

tasks.withType(Javadoc).configureEach { task ->
    def opts = task.options
    opts.encoding = 'UTF-8'
    opts.charSet = 'UTF-8'
    opts.locale = 'en'
    // Protobuf/gRPC generated sources often contain Javadoc patterns that trigger doclint warnings.
    // We don't want these warnings (or doclint strictness) to fail or spam service builds.
    task.failOnError = false
    opts.addStringOption('Xdoclint:none', '-quiet')

    // Whitelist only application source packages (exclude generated gRPC/protobuf sources)
    include '**/ai/pipeline/connector/**'
    include '**/ai/pipestream/connector/**'

    // Extra safety: proto-toolchain adds generated sources under build/generated/** which can
    // share application packages and still match the includes above.
    exclude '**/generated/source/proto/**'
}

// Publishing configuration
publishing {
    publications {
        maven(MavenPublication) { publication ->
            from components.java
            pom {
                name.set('Connector Intake Service')
                description.set('Intake and processing service for connector data in the Pipeline platform')
                url.set('https://github.com/ai-pipestream/connector-intake-service')

                licenses {
                    license {
                        name.set('MIT License')
                        url.set('https://opensource.org/licenses/MIT')
                    }
                }

                developers {
                    developer {
                        id.set('krickert')
                        name.set('Pipestream Platform Team')
                    }
                }

                scm {
                    connection.set('scm:git:git://github.com/ai-pipestream/connector-intake-service.git')
                    developerConnection.set('scm:git:ssh://github.com/ai-pipestream/connector-intake-service.git')
                    url.set('https://github.com/ai-pipestream/connector-intake-service')
                }
            }
        }
    }

    repositories {
        mavenLocal()

        // GitHub Packages
        def ghRepo = System.getenv('GITHUB_REPOSITORY') ?: 'ai-pipestream/connector-intake-service'
        def ghActor = System.getenv('GITHUB_ACTOR') ?: (project.findProperty('gpr.user') ?: System.getenv('GPR_USER'))
        def ghToken = System.getenv('GITHUB_TOKEN') ?: (project.findProperty('gpr.key') ?: System.getenv('GPR_TOKEN'))
        if (ghActor && ghToken) {
            maven {
                name = 'GitHubPackages'
                url = uri("https://maven.pkg.github.com/${ghRepo}")
                credentials {
                    username = ghActor
                    password = ghToken
                }
            }
        }
    }
}

// GPG Signing (uses org-level secrets)
signing {
    def signingKey = System.getenv("GPG_PRIVATE_KEY")
    def signingPassword = System.getenv("GPG_PASSPHRASE")

    if (signingKey && signingPassword) {
        useInMemoryPgpKeys(signingKey, signingPassword)
        sign publishing.publications.maven
    }
}

// Ensure signing tasks run before NMCP publication
afterEvaluate {
    tasks.matching { it.name.startsWith("nmcpZipAllPublications") || it.name.startsWith("nmcpPublishAllPublicationsToCentralPortal") }
        .configureEach { nmcpTask ->
            nmcpTask.dependsOn(tasks.withType(Sign))
        }
}

// Maven Central publishing via NMCP
nmcp {
    publishAllPublicationsToCentralPortal {
        username = providers.environmentVariable("MAVEN_CENTRAL_USERNAME")
        password = providers.environmentVariable("MAVEN_CENTRAL_PASSWORD")
        publishingType = "AUTOMATIC"
    }
}
