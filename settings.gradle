pluginManagement {
    repositories {
        gradlePluginPortal()
    }
}

rootProject.name = 'connector-intake-service'

dependencyResolutionManagement {
    versionCatalogs {
        libs {
            // Import version catalog from published BOM
            from("ai.pipestream:pipeline-bom-catalog:0.2.7")
        }
    }

    repositories {
        // Prefer Maven Central but allow locally published releases and snapshots for development
        mavenLocal {
            mavenContent {
                releasesOnly()
                snapshotsOnly()
            }
            content {
                includeGroupByRegex "ai\\.pipestream(\\..*)?"
            }
        }

        // Maven Central (always available, default for public builds)
        mavenCentral()

        // Maven Central Snapshots for consuming published SNAPSHOTs
        maven {
            url = uri('https://central.sonatype.com/repository/maven-snapshots/')
            mavenContent {
                snapshotsOnly()
            }
        }

        // GitHub Packages for ai.pipestream catalog and dependencies
        def githubToken = providers.environmentVariable("GITHUB_TOKEN")
        if (githubToken.isPresent()) {
            maven {
                url = uri("https://maven.pkg.github.com/ai-pipestream/bom")
                credentials {
                    username = providers.environmentVariable("GITHUB_ACTOR").getOrElse("github-actions")
                    password = githubToken.get()
                }
                content {
                    includeGroupByRegex "ai\\.pipestream(\\..*)?"
                }
            }
        }

        // Apache snapshots for Tika 4.x pre-release (default for public builds)
        if (System.getenv('CI_ENV') != 'internal') {
            maven {
                url = uri('https://repository.apache.org/content/repositories/snapshots/')
                allowInsecureProtocol = false
                content {
                    includeGroupByRegex "org\\.apache\\.tika(\\..*)?"
                }
            }
        }
    }
}
