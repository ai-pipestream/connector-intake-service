pluginManagement {
    repositories {
        gradlePluginPortal()
    }
}

rootProject.name = 'connector-intake-service'

dependencyResolutionManagement {
    versionCatalogs {
        libs {
            // Import version catalog from published BOM
            // Available on Maven Central and GitHub Packages
            // Version controlled via gradle.properties
            from("ai.pipestream:pipestream-bom-catalog:0.7.8")
        }
    }

    repositories {
        // Maven Local for local development (releases only for ai.pipestream)
        mavenLocal {
            mavenContent {
                releasesOnly()
            }
            content {
                includeGroupByRegex "ai\\.pipestream(\\..*)?"
            }
        }

        // Maven Central (always available, default for public builds)
        mavenCentral()

        // Maven Central Snapshots for consuming published SNAPSHOTs
        maven {
            url = uri('https://central.sonatype.com/repository/maven-snapshots/')
            mavenContent {
                snapshotsOnly()
            }
        }

        // GitHub Packages for ai.pipestream catalog and dependencies
        def githubToken = providers.environmentVariable("GITHUB_TOKEN")
            .orElse(providers.gradleProperty("gpr.key"))
            .orElse(providers.environmentVariable("GPR_TOKEN"))

        def githubActor = providers.environmentVariable("GITHUB_ACTOR")
            .orElse(providers.gradleProperty("gpr.user"))
            .orElse(providers.environmentVariable("GPR_USER"))

        if (githubToken.isPresent() && githubActor.isPresent()) {
            maven {
                url = uri("https://maven.pkg.github.com/ai-pipestream/bom")
                credentials {
                    username = githubActor.get()
                    password = githubToken.get()
                }
                content {
                    includeGroupByRegex "ai\\.pipestream(\\..*)?"
                }
            }
        }

        // Apache snapshots for Tika 4.x pre-release (default for public builds)
        if (System.getenv('CI_ENV') != 'internal') {
            maven {
                url = uri('https://repository.apache.org/content/repositories/snapshots/')
                allowInsecureProtocol = false
                content {
                    includeGroupByRegex "org\\.apache\\.tika(\\..*)?"
                }
            }
        }
    }
}
